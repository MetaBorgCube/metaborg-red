module trans/dynamics/Expressions

imports
src-gen/ds-signatures/Smalltalk-sig
src-gen/ds-signatures/Base-sig

trans/runtime/values
trans/runtime/store

signature

  constructors
  	NilV: V
    NumV: Int -> V
    BoolV: Bool -> V
    StringV: String -> V
  	BinaryMessageCall: String * V -> Message
    UnaryMessageCall: String -> Message
    KeywordValuePair: String * V ->	KeywordMessageElement
    KeywordMessageCall: List(String) * List(V) -> Message
    MethodExpressionV: BinaryMessageOperand * List(BinaryMessage) -> V
    
  arrows
    Expression --> V
  	
  	Literal --> V
  	Reference --> V
  	Operand --> V
  	UnaryMessage --> Message
  	BinaryMessage --> Message
  	KeywordMessageSegment --> KeywordMessageElement
  	KeywordMessage --> List(Message)
  	MessageChain -->  List(Message)
  	UnaryMessageChain --> List(Message)
  	BinaryMessageOperand --> V
  	UnaryMessageSelector --> String
  	Keyword --> String
  	KeywordMessageArgument --> V
  	Statement --> V
  	ConstantReference --> V
  	ExecutableCodeRest --> V
  	Message --> Message
  	BlockLiteral --> V
  	V --> V
  	List(BinaryMessage) --> List(Message)
  	List(UnaryMessage) --> List(Message)
  	List(String) --> List(String)
  	KeywordMessageMap(List(KeywordMessageSegment), Message) --> Message
  	ExpI(V, List(Message)) --> V
//    DynamicDispatch: List(Message) -> V
  native operators
    parseI: String -> Int
    num-call-native: String * V * V -> V
    bool-call-native: String * V * V -> V
    num-call-native: String * V -> V
    bool-call-native: String * V -> V
    num-calls-native: List(String) * List(V) * V -> V
   	printV: V -> V
  components
  	L : List(Message)

rules

	SingletonExpression(operand) --> operand.	
	
	Expression(operand, chain, cascaded) --> result
	where 
		ExpI(operand, chain) --> result.
		
	ExpI(operand, [headMessage | tailMessages]) --> v4
		where
		case headMessage of {
		UnaryMessageCall(messageName) =>
			case operand of {
			NumV(_) => 
				num-call-native(messageName, operand) => v3;
				ExpI(v3, tailMessages) --> v4
			BoolV(_) =>		
				bool-call-native(messageName, operand) => v3;
				ExpI(v3, tailMessages) --> v4
			}	
		BinaryMessageCall(messageName, v2) =>
			case operand of {
			NumV(_) => 
				num-call-native(messageName, operand, v2) => v3;
				ExpI(v3, tailMessages) --> v4
			BoolV(_) =>		
				bool-call-native(messageName, operand, v2) => v3;
				ExpI(v3, tailMessages) --> v4
			}
		KeywordMessageCall(keywords, values) =>
			case operand of {
			NumV(_) => 
				num-calls-native(keywords, values, operand) => v3;
				ExpI(v3, tailMessages) --> v4
			BoolV(true) =>
				keywords => [a, b];
				case a of {
					"ifTrue" =>
						case b of {
							"ifFalse" => 
								values[0] => methodV;
								methodV --> v4
						}
					"ifFalse" =>
						case b of {
							"ifTrue" =>
								values[1] => methodV;
								methodV --> v4
						}
				}
			BoolV(false) =>
				keywords => [a, b];
				case a of {
					"ifTrue" =>
						case b of {
							"ifFalse" => 
								values[1] => methodV;
								methodV --> v4
						}
					"ifFalse" =>
						case b of {
							"ifTrue" =>
								values[0] => methodV;
								methodV --> v4
						}
				}
			}
		}
			
	ExpI(e3, []) --> e3.
	
		
	Literal(e1) --> e1.
	
	ReferenceOperand(ref) --> ref.
	
	ReferenceID(id) --> readVar(id).
		
	IntegerLiteral(s) --> NumV(parseI(s)).
	ConstantReference(bool) --> bool
		where bool --> BoolV(b).
	
	True() --> BoolV(true).
	False() --> BoolV(false).
	
//	NumV(a) --> NumV(a).
//	BoolV(b) --> BoolV(b).
	
	BinaryMessage(selector, operand) --> BinaryMessageCall(selector, operand).
		
	BinaryMessageChain([bHead | bTail]) --> [b1 | BinaryMessageChain(bTail)]
		where 
		bHead --> b1.
		
	BinaryMessageChain([]) --> [].
	
	BinaryMessageOperand(operand, unarychain) --> ExpI(operand, unaryMessages)
		where
		unarychain --> unaryMessages. 
	
	BinaryMessageChainWithKeyword(binaryMessages, keywordMessage) --> binMessages ++ keyMessage
		where
			BinaryMessageChain(binaryMessages) --> binMessages;
			keywordMessage --> keyMessage.
		
	UnaryMessageChain([uHead | uTail]) --> [u1 | UnaryMessageChain(uTail)]
	where
		uHead --> u1.
		
	UnaryMessageChain([]) --> [].
	
	UnaryMessage(selector) --> UnaryMessageCall(selector).
	
	UnaryMessageSelector(identifier) --> identifier.
	
	UnaryBinaryMessageChain(unaryMessages, binaryMessages) --> uChain ++ bChain
	where
		UnaryMessageChain(unaryMessages) --> uChain;
	 	BinaryMessageChain(binaryMessages) --> bChain.
		
	
	UnaryMessageChainWithKeyword(unaryMessages, binaryMessages, keywordMessage) --> uList ++ kList
	where
		UnaryBinaryMessageChain(unaryMessages, binaryMessages) --> uList;
		keywordMessage --> kList.
	
	KeywordMessageChain(keywordMessage) --> keywordMessage.
	
	KeywordMessage(messageSegment) --> [KeywordMessageCall(keys, values)]
		where
			KeywordMessageMap(messageSegment, KeywordMessageCall([],[])) => KeywordMessageCall(keys, values).
			
	KeywordMessageMap([messageSegmentHead | messageSegmentTail], KeywordMessageCall(curKeys, curVals)) --> message
		where
		messageSegmentHead --> KeywordValuePair(key, value);
		KeywordMessageMap(messageSegmentTail, KeywordMessageCall([key | curKeys], [value | curVals])) => message.
		
	KeywordMessageMap([], KeywordMessageCall(keys, vals)) --> KeywordMessageCall(keys, vals).
	
	KeywordMessageSegment(keyword, keywordMessageArgument) --> KeywordValuePair(key, MethodExpressionV(binOperand, binMessages))
		where
		keyword --> key;
		keywordMessageArgument => MethodExpressionV(binOperand, binMessages).
		
		
	KeywordMessageArgument(binaryMessageOperand, binaryMessages) --> MethodExpressionV(binaryMessageOperand, binaryMessages).
	MethodExpressionV(binaryMessageOperand, binaryMessages) --> ExpI(binaryMessageOperand, BinaryMessageChain(binaryMessages)).
		
	Keyword(key) --> key.	

    